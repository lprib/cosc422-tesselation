#version 400

layout(quads, equal_spacing, ccw) in;

uniform mat4 mvp_matrix;
uniform sampler2D height_map;

// x: xmin, y: xmax, z: zmin, w: zmax
uniform vec4 object_bounds;

void main()
{
    vec4 posn;
    vec2 tcoord;


    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;

    posn = (1-u) * (1-v) *  gl_in[0].gl_Position
        + u * (1-v) *       gl_in[1].gl_Position
        + u * v *           gl_in[2].gl_Position
        + (1-u) * v *       gl_in[3].gl_Position;

    tcoord.s = (posn.x - object_bounds.x) / (object_bounds.y - object_bounds.x);
    tcoord.t = (posn.z - object_bounds.z) / (object_bounds.w - object_bounds.z);

    float height = texture(height_map, tcoord).r;
    posn.y = height * 10.0;

    gl_Position = mvp_matrix * posn;
}

// vim: set ft=glsl:

